developer=https://withknown.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.9.9
pkgrel=5
pkgdesc="Known: Indieweb publishing platform for everyone"
arch=('any')
license=('Apache')
groups=()
depends=()
if [[ -z ${USE_GIT} ]]; then
    source=( "http://assets.withknown.com/releases/known-$pkgver.tgz" )
    sha512sums=('ccfe13340aa07a259504570cf0c090d272aed68e9cab071e9f5542b1dc27d46a4f9f113f3f4a3028b356fbc4bd82482a957a9f6aa2f1b9abb423c7f0d278e46f')
else
    source=( "git+https://github.com/idno/Known.git" );
    sha512sums=('SKIP')
fi
releasepage=( 'https://withknown.com/opensource/' )
options=('!strip')

package() {
# Manifest
    mkdir -p $pkgdir/var/lib/ubos/manifests
    install -m0644 $startdir/ubos-manifest.json $pkgdir/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p $pkgdir/srv/http/_appicons/$pkgname
    install -m644 $startdir/appicons/{72x72,144x144}.png $pkgdir/srv/http/_appicons/$pkgname/

# Templates
    mkdir -p $pkgdir/usr/share/${pkgname}/tmpl
    install -m755 $startdir/tmpl/{config.ini,htaccess,htaccess-site}.tmpl $pkgdir/usr/share/${pkgname}/tmpl/

# Data directory
    mkdir -p $pkgdir/var/lib/${pkgname}

# Code
    mkdir -p $pkgdir/usr/share/${pkgname}/known
    if [[ -z ${USE_GIT} ]]; then
        rsync -r -a --filter '- ._*' $srcdir/* $pkgdir/usr/share/${pkgname}/known/
    else
        rsync -r -a --filter '- ._*' $srcdir/Known/* $pkgdir/usr/share/${pkgname}/known/
    fi
}
